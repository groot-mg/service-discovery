plugins {
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id "io.freefair.lombok"
    id 'java'
}

ext {
    cucumberVersion = '7.18.0'
    jacksonDataBinding = '2.15.3'
    prometheusScraperVersion = '0.23.0.Final'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation project(":service-discovery-app")

    testImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"

    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDataBinding}"
    testImplementation "org.hawkular.agent:prometheus-scraper:${prometheusScraperVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.google.guava:guava:32.1.3-jre"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber {
    def profile = findProperty("spring.profiles.active") ?: "local"
    dependsOn processTestResources, compileTestJava

    doLast {
        javaexec {
            systemProperties = ['spring.profiles.active': profile]
            main = "io.cucumber.core.cli.Main"
            classpath = project.sourceSets.test.runtimeClasspath
            args = ['--tags', 'not @ignore',
                    '--plugin', 'pretty',
                    '--plugin', 'html:reports/report.html',
                    '--glue', 'com.generoso.ft.sd',
                    'classpath:features']
        }
    }
}

bootJar.enabled = false
jar.enabled = false