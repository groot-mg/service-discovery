plugins {
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.lombok)
    alias(libs.plugins.java)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.cucumber.spring)
    implementation(libs.cucumber.java)

    implementation(libs.jackson.databing)
    implementation(libs.prometheus.scraper)

    implementation(libs.spring.boot.starter.test)
    implementation(libs.google.guava)
}

configurations {
    cucumberRuntime {
        extendsFrom implementation
    }
}

tasks.register('cucumber') {
    def profile = findProperty("spring.profiles.active") ?: "local"
    dependsOn processTestResources, compileTestJava

    doLast {
        javaexec {
            systemProperties = ['spring.profiles.active': profile]
            main = "io.cucumber.core.cli.Main"
            classpath = project.sourceSets.test.runtimeClasspath
            args = ['--tags', 'not @ignore',
                    '--plugin', 'pretty',
                    '--plugin', 'html:reports/report.html',
                    '--glue', 'com.generoso.ft.sd',
                    'classpath:features']
        }
    }
}

bootJar.enabled = false
jar.enabled = false
