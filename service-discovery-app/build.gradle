import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id "com.gorylenko.gradle-git-properties" version '2.4.1'
    id 'pl.allegro.tech.build.axion-release' version '1.17.2'
    id 'io.freefair.lombok'
    id 'application'
    id 'java'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.8"
}

ext {
    eurekaServerVersion = '4.0.3'
    owaspEncoderVersion = '1.2.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

scmVersion {
    tag { prefix = rootProject.name + '-' }
    versionIncrementer 'incrementMinor'
}
project.version = scmVersion.version

bootJar {
    archiveFileName = "${rootProject.name}.jar"
}

springBoot {
    buildInfo {
        properties {
            name = rootProject.name
            version = project.version
            additional = [
                    host           : InetAddress.localHost.hostName,
                    hostAddress    : InetAddress.localHost.getHostAddress(),
                    operatingSystem: DefaultNativePlatform.currentOperatingSystem.getInternalOs().toString()
            ]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-server:${eurekaServerVersion}" // not resolved by dependency-management plugin
    implementation 'jakarta.servlet:jakarta.servlet-api'
    implementation "org.owasp.encoder:encoder:${owaspEncoderVersion}"
    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/generoso/sd/ServiceDiscoveryServerApplication.class"
            ])
        }))
    }
    reports {
        xml.required = true
    }
}
